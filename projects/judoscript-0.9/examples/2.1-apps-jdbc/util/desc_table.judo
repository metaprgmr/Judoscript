usage {
  minArgs = 2;
  args    = 'table_name, jdbc_url [, username, password]';
  author  = 'James Huang';
  created = '2001-11-12';
  lastMod = '2002-08-10';
  desc    = [[*
    Use the describe() function to get the structure of a database table,
    then use printTable statement to print out in tabular format.
  *]];
}

table    = #args[0];
url      = #args[1];
username =  (#args.length > 2) ? #args[2] : null;
password =  (#args.length > 3) ? #args[3] : null;

connect to url, username, password;
a = describe(table);

println 'Table: ', table, nl;
println 'Column         Type            Precision   Scale Nullable';
println '-------------- ------------ ------------ ------- ----------';

printTable a for
  column('name') :<15!,
  column('type') :<13!,
  column('precision')   :>12,
  column('scale')       :>8, ' ',
  column('nullable');

disconnect();
