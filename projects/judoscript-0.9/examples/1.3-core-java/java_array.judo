usage {
  author  = 'James Huang';
  created = '2001-11-12';
  lastMod = '2002-08-10';
  desc    = [[*
    Creates and prints 1D and 3D arrays of Java primitive types and objects.
    All arrays are initialized.
  *]];
}

!JavaBaseClass #Object, #String, #Boolean, #Float

sa = new java::#String[] { "A", "B", "C", "D" };
println 'sa.length = ', sa.length;

ia = new java::int[][][] {
        { { 1, 2, 3 }, {  4, 5, 6 } },
        { { 7, 8, 9 }, { 10 } }
      };
sia = new java::short[][][] {
        { { 1, 2, 3 }, {  4, 5, 6 } },
        { { 7, 8, 9 }, { 10 } }
      };
fa = new java::#Float[][][] {
        { { 1.5, 2.5, 3.5 }, {  4.5, 5.5, 6.5 } },
        { { 7.5, 8.5, 9.5 }, { 10.5 } }
      };
ba = new java::boolean[][][] {
        { { true, false, true }, { false, true, true } },
        { { true, true,  true }, { true, true } }
      };
Ba = new java::#Boolean[][][] {
        { { true, false, true }, { false, true, true } },
        { { true, true,  true }, { true, true } }
      };
ca = new java::char[][][] {
        { { 'A', 'B', 'C' }, { 'D', 'E', 'F' } },
        { { 'G', 'H', 'I' }, { 'J', 'K' } }
      };
oa = new java::#Object[][][] {
        { { 'a', 'b', 'c' }, { 'd', 'e', 'f' } },
        { { 'g', 'h', 'i' }, { 'j', 'k' } }
      };

{
  . ia;
catch: $_.printInternalStackTrace();
}

print1d sa;
print3d ia;
print3d sia;
print3d fa;
print3d ba;
print3d Ba;
print3d ca;
print3d oa;

//////////////////////////////////
// functions
//

function print1d a
{
  println '----- print 1D: ', a;
  for x in a { println x; }
}

function print3d a
{
  println '----- print 3D: ', a;
  for i from 0 to a.length-1 {
    for j from 0 to a[i].length-1 {
      for k from 0 to a[i][j].length-1 {
        println i, ' ', j, ' ', k, ' => ', a[i][j][k];
      }
    }
  }
}
